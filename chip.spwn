extract import "font.spwn"
import "ext.spwn"

type @chip

/* Lets just omit the sound timer as we can't play sounds */
impl @chip {
    v: @array,
    stack: @array,
    i: @number,
    pc: @number,
    sp: @number,
    st: @number,
    dt: @number,
    ram: @array,
    vram: @array,
    rnd: @array,
    draw: @bool,
    debug: @bool,
    dbg_out: @array,

    new: (location: @string, debug: @bool) {
        let ram = [0] * 0x1000
        for i in 0..font.length { ram[i] = font[i] }
        rom = $.readfile(location, "bin")
        for i in 0..rom.length { ram[i + 0x200] = rom[i] }
        let rnd = []
        for i in 0..256 { rnd.push(i) }

        return @chip::{
            v: [0] * 16,
            stack: [0] * 16,
            i: 0x0000,
            pc: 0x200,
            sp: 0,
            st: 0,
            dt: 0,
            ram: ram,
            vram: [[0] * 64] * 32,
            rnd: rnd,
            draw: false,
            debug: debug,
            dbg_out: []
        }
    },

    do_draw: (self) {
        let line = []
        for y in self.vram {
            line.push([])
            for x in y {
                pixel = "\u{1b}[2;47m " if x == 1 else "\u{1b}[2;40m "
                line[-1].push(pixel)
            }
            line[-1] = line[-1].join()
        }
        
        // print is slow as fuck on some devices
        // so less printing = better
        $.print(
        "\u{1b}[2J\u{1b}[H\u{1b}[0m"+
        self.dbg_out.join("\n")+
        line.join("\n")) // bombie code be like
        
        self.dbg_out = []
        self.draw = false
    },

    cycle: (self) {
        self.dec_timer()
        opcode = self.opcode()
        self.execute(opcode)
    },

    opcode: (self) {
        return self.ram[self.pc].shift_left(8).or(self.ram[self.pc + 1])
    },

    dec_timer: (self) {
        if self.dt > 0 { self.dt -= 1 }
    },

    execute: (self, opcode: @number) {
        byte = [
            opcode.and(0xF000).shift_right(12),
            opcode.and(0x0F00).shift_right(8),
            opcode.and(0x00F0).shift_right(4),
            opcode.and(0x000F)
        ]

        nnn = opcode.and(0x0FFF)
        nn = opcode.and(0x00FF)
        n = byte[3]
        x = byte[1]
        y = byte[2]
        
        if byte[0] == 0x0 && byte[1] == 0x0 && byte[2] == 0xE && byte[3] == 0 {
            if self.debug == true { self.dbg_out.push("CLS") }
            for y in 0..32 {
                for x in 0..64 {
                    self.vram[y][x] = 0
                }
            }
            self.pc += 2
        } else if byte[0] == 0x0 && byte[1] == 0x0 && byte[2] == 0xE && byte[3] == 0xE {
            if self.debug == true { self.dbg_out.push("RET") }
            self.sp -= 1
            self.pc = self.stack[self.sp]
        } else if byte[0] == 0x1 {
            if self.debug == true { self.dbg_out.push("JP {}".fmt(nnn)) }
            self.pc = nnn
        } else if byte[0] == 0x2 {
            if self.debug == true { self.dbg_out.push("CALL {}".fmt(nnn)) }
            self.stack[self.sp] = self.pc + 2
            self.sp += 1
            self.pc = nnn
        } else if byte[0] == 0x3 {
            if self.debug == true { self.dbg_out.push("SE V{}, {}".fmt([x, nn])) }
            if self.v[x] == nn { self.pc += 2 }
            self.pc += 2
        } else if byte[0] == 0x4 {
            if self.debug == true { self.dbg_out.push("SNE V{}, {}".fmt([x, nn])) }
            if self.v[x] != nn { self.pc += 2 }
            self.pc += 2
        } else if byte[0] == 0x5 && byte[3] == 0x0 {
            if self.debug == true { self.dbg_out.push("SE V{}, V{}".fmt([x, y])) }
            if self.v[x] == self.v[y] { self.pc += 2 }
            self.pc += 2
        } else if byte[0] == 0x6 {
            if self.debug == true { self.dbg_out.push("LD V{}, {}".fmt([x, nn])) }
            self.v[x] = nn
            self.pc += 2
        } else if byte[0] == 0x7 {
            if self.debug == true { self.dbg_out.push("ADD V{}, {}".fmt([x, nn])) }
            self.v[x] = self.v[x].overflowing_add(nn)[0]
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x0 {
            if self.debug == true { self.dbg_out.push("LD V{}, V{}".fmt([x, y])) }
            self.v[x] = self.v[y]
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x1 {
            if self.debug == true { self.dbg_out.push("OR V{}, V{}".fmt([x, y])) }
            self.v[x] = self.v[x].or(self.v[y])
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x2 {
            if self.debug == true { self.dbg_out.push("AND V{}, V{}".fmt([x, y])) }
            self.v[x] = self.v[x].and(self.v[y])
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x3 {
            if self.debug == true { self.dbg_out.push("XOR V{}, V{}".fmt([x, y])) }
            self.v[x] = self.v[x].xor(self.v[y])
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x4 {
            if self.debug == true { self.dbg_out.push("ADD V{}, V{}".fmt([x, y])) }
            new = self.v[x].overflowing_add(self.v[y])
            self.v[x] = new[0]
            self.v[0xF] = 1 if new[1] == true else 0
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x5 {
            if self.debug == true { self.dbg_out.push("SUB V{}, V{}".fmt([x, y])) }
            new = self.v[x].overflowing_sub(self.v[y])
            self.v[x] = new[0]
            self.v[0xF] = 0 if new[1] == true else 1
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x6 {
            if self.debug == true { self.dbg_out.push("SHR V{}, V{}".fmt([x, y])) }
            self.v[0xF] = self.v[x].lsb()
            self.v[x] = self.v[x].overflowing_div(2)[0]
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0x7 {
            if self.debug == true { self.dbg_out.push("SUBN V{}, V{}".fmt([x, y])) }
            new = self.v[y].overflowing_sub(self.v[x])
            self.v[0xF] = 0 if new[1] == true else 1
            self.v[x] = new[0]
            self.pc += 2
        } else if byte[0] == 0x8 && byte[3] == 0xE {
            if self.debug == true { self.dbg_out.push("SHL V{}, V{}".fmt([x, y])) }
            self.v[0xF] = self.v[x].msb()
            self.v[x] = self.v[x].overflowing_mul(2)[0]
            self.pc += 2
        } else if byte[0] == 0x9 && byte[3] == 0x0 {
            if self.debug == true { self.dbg_out.push("SNE V{}, V{}".fmt([x, y])) }
            if self.v[x] != self.v[y] { self.pc += 2 }
            self.pc += 2
        } else if byte[0] == 0xA {
            if self.debug == true { self.dbg_out.push("LD I, {}".fmt(nnn)) }
            self.i = nnn
            self.pc += 2
        } else if byte[0] == 0xB {
            if self.debug == true { self.dbg_out.push("JP V0, {}".fmt(nnn)) }
            self.i = nnn + self.v[0]
        } else if byte[0] == 0xC {
            if self.debug == true { self.dbg_out.push("RND V{}, {}".fmt([x, nn])) }
            self.v[x] = $.random(self.rnd).and(nn)
            self.pc += 2
        } else if byte[0] == 0xD {
            if self.debug == true { self.dbg_out.push("DRW V{}, V{}, {}".fmt([x, y, n])) }
            self.v[0xF] = 0
            for byte in 0..n {
                yu = ((self.v[y] + byte) % 32).floor()
                for bit in 0..8 {
                    xu = ((self.v[x] + bit) % 64).floor()
                    color = (self.ram[self.i + byte].shift_right(7 - bit)).and(1)
                    self.v[0xF] = self.v[0xF].or(color.and(self.vram[yu][xu]))
                    self.vram[yu][xu] = self.vram[yu][xu].xor(color)
                }
            }
            self.draw = true
            self.pc += 2
        } else if byte[0] == 0xE && byte[2] == 0x9 && byte[3] == 0xE {
            if self.debug == true { self.dbg_out.push("SKP V{}".fmt(x)) }
            self.pc += 2
        } else if byte[0] == 0xE && byte[2] == 0xA && byte[3] == 0x1 {
            if self.debug == true { self.dbg_out.push("SKNP V{}".fmt(x)) }
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x0 && byte[3] == 0x7 {
            if self.debug == true { self.dbg_out.push("LD V{}, DT".fmt(x)) }
            self.v[x] = self.dt
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x0 && byte[3] == 0xA {
            if self.debug == true { self.dbg_out.push("LD V{}, K".fmt(x)) }
            self.v[x] = 0x0
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x1 && byte[3] == 0x5 {
            if self.debug == true { self.dbg_out.push("LD DT, V{}".fmt(x)) }
            self.dt = self.v[x]
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x1 && byte[3] == 0x8 {
            if self.debug == true { self.dbg_out.push("LD ST, V{}".fmt(x)) }
            self.st = self.v[x]
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x1 && byte[3] == 0xE {
            if self.debug == true { self.dbg_out.push("ADD I, V{}".fmt(x)) }
            self.i += self.v[x]
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x2 && byte[3] == 0x9 {
            if self.debug == true { self.dbg_out.push("LD F, V{}".fmt(x)) }
            self.i = self.v[x] * 5
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x3 && byte[3] == 0x3 {
            if self.debug == true { self.dbg_out.push("LD B, V{}".fmt(x)) }
            self.ram[self.i] = self.v[x] / 100
            self.ram[self.i + 1] = (self.v[x] % 100) / 10
            self.ram[self.i + 2] = self.v[x] % 10
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x5 && byte[3] == 0x5 {
            if self.debug == true { self.dbg_out.push("LD [I], V{}".fmt(x)) }
            for i in 0..x + 1 {
                self.ram[self.i + i] = self.v[i]
            }
            self.pc += 2
        } else if byte[0] == 0xF && byte[2] == 0x6 && byte[3] == 0x5 {
            if self.debug == true { self.dbg_out.push("LD V{}, [I]".fmt(x)) }
            for i in 0..x + 1 {
                self.v[i] = self.ram[self.i + i]
            }
            self.pc += 2
        } else {
            throw "Unknown opcode"
        }
    }
}